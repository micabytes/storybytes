// Top-level build file where you can add configuration options common to all sub-projects/modules.
// To check versions of libraries, run:
// gradlew dependencyUpdates
apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext.gradle_versions_version = '0.21.0'
    ext.android_ktx_version = '1.0.1'
    ext.android_navigation_version = '1.0.0-rc02'
    ext.android_tools_version = '3.3.1'
    ext.androidx_appcompat_version = '1.0.2'
    ext.androidx_constraint_version = '1.1.3'
    ext.androidx_legacy_version = '1.0.0'
    ext.blade_ink_version = '0.5.1'
    ext.kotlin_version = '1.3.21'

    repositories {
        google()
        jcenter()
        
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_tools_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:$gradle_versions_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }

}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
